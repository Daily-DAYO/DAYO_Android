plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
    id 'io.sentry.android.gradle'
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
def NATIVE_APP_KEY = properties.getProperty('NATIVE_APP_KEY')
def ADS_APPLICATION_ID = properties.getProperty('ADS_APPLICATION_ID')
def keystorePropertiesFile = rootProject.file("app/keystore-release.properties")

sentry {
    setIncludeProguardMapping(true)
    setAutoUploadProguardMapping(true)
    setUploadNativeSymbols(false)
    setIncludeNativeSources(false)
    setAutoUpload(true)
}

kotlin {
    jvmToolchain(17)
}

android {
    compileSdk rootProject.ext.compileSdkVersion

    defaultConfig {
        applicationId "com.daily.dayo"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 20000
        versionName "2.0.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField ("String", "NATIVE_APP_KEY", properties['NATIVE_APP_KEY_STR'])
        manifestPlaceholders = [
                NATIVE_APP_KEY    : NATIVE_APP_KEY,
                ADS_APPLICATION_ID: ADS_APPLICATION_ID
        ]
    }

    signingConfigs {
        release {
            def keystoreProperties = new Properties()
            keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

            storeFile file(keystoreProperties['storeFile'])
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storePassword keystoreProperties['storePassword']
        }
    }

    buildFeatures {
        // AGP 9.0부터 buildConfig가 사라짐에 따라 8.0에서의 임시방편
        buildConfig = true
    }

    buildTypes {
        debug {
            minifyEnabled false
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
            resValue "string", "app_name", "DAYO (Debug)"
        }
        release {
            resValue "string", "app_name", "DAYO"
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    flavorDimensions = ["environment"]
    productFlavors {
        dev {
            dimension "environment"
            buildConfigField("String", "BASE_URL", properties['BASE_URL_DEV'])
        }
        prod {
            dimension "environment"
            buildConfigField("String", "BASE_URL", properties['BASE_URL_PROD'])
        }
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17.toString()
    }
    buildFeatures {
        dataBinding true
    }
    namespace 'com.daily.dayo'
}

dependencies {
    // multiModule
    implementation project(':presentation')
    implementation project(':domain')
    implementation project(':data')

    def sentry_version = "7.16.0"
    def glide_version = "4.15.1"

    // Hilt
    implementation "com.google.dagger:hilt-android:2.44"
    kapt "com.google.dagger:hilt-android-compiler:2.44"
    // Kakao sdk
    implementation "com.kakao.sdk:v2-user:2.9.0"
    // Glide
    implementation "com.github.bumptech.glide:glide:$glide_version"
    kapt "com.github.bumptech.glide:compiler:$glide_version"
    // Sentry
    implementation "io.sentry:sentry-android:$sentry_version"
    implementation 'org.slf4j:slf4j-nop:1.7.25'
    // Firebase
    implementation 'com.google.firebase:firebase-crashlytics-ktx'
    implementation 'com.google.firebase:firebase-analytics-ktx'

    // Google Ads
    implementation 'com.google.android.gms:play-services-ads:23.6.0'
}